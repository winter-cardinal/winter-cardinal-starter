plugins {
	id "java"
	id "eclipse"
	id "org.springframework.boot" version "2.7.18"
	id "io.spring.dependency-management" version "1.1.5"
	id "io.freefair.lombok" version "6.4.3"
	id "com.diffplug.spotless" version "6.25.0"
}

// GENERAL
def packageJson = new groovy.json.JsonSlurper().parseText(file("package.json").text);
def packageName = packageJson.name;
def slashIndex = packageName.indexOf("/");
if (0 <= slashIndex) {
	group = packageName.substring(1, slashIndex);
	archivesBaseName = packageName.substring(slashIndex + 1);
	version = packageJson.version;
} else {
	group = "com";
	archivesBaseName = packageName;
	version = packageJson.version;
}

// REPOSITORY
repositories {
	mavenCentral()
}

compileJava {
	options.release = 17
}

// JAR
jar {
	enabled = false
}

// SPRING BOOT
springBoot {
	mainClass = "app.Main"
}

// SPOTLESS
spotless {
	java {
		palantirJavaFormat()
		indentWithTabs()
		lineEndings "UNIX"
		toggleOffOn()
	}
}

// DEPENDENCIES
dependencies {
	implementation "com.github.winter-cardinal:winter-cardinal:1.0.3"
	implementation "org.springframework.boot:spring-boot-starter-web"
	implementation "org.springframework.boot:spring-boot-starter-data-jpa"
	implementation "org.springframework.boot:spring-boot-starter-thymeleaf"
	implementation "org.springframework.boot:spring-boot-starter-security"
	implementation "org.springframework.boot:spring-boot-starter-validation"
	implementation "org.springframework.boot:spring-boot-starter-websocket"
	implementation "net.java.dev.jna:jna:5.6.0"
	implementation "net.java.dev.jna:jna-platform:5.6.0"
	implementation "org.glassfish.jaxb:jaxb-runtime:2.3.2"
	runtimeOnly "com.h2database:h2:2.1.210"
}
